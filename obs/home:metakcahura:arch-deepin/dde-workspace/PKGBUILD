# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=dde-workspace
pkgname=('dde-workspace' 'lightdm-deepin-greeter')
pkgver=20140522.a4e32db
pkgrel=1
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
groups=('deepin')
makedepends=('cmake' 'go' 'coffee-script' 'go-gettext-go' 'webkitgtk'
             'libdeepin-webkit' 'lightdm' 'cairo' 'dbus-glib' 'python2')
source=("${pkgbase}-${pkgver}.tar.xz" 'default-display-manager')
sha256sums=('SKIP' '3667d6cce9a00f0b8c8bc27250b2ceaf421382d3b4e687a5bcea4c64292030df')

build() {
  cd "${pkgbase}-${pkgver}"

  [[ -d build ]] && rm -rvf build/
  mkdir build && cd build

  #export PYTHON=/usr/bin/python2
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make

  gcc -o ../debian/default-terminal ../debian/default-terminal.c `pkg-config --libs --cflags glib-2.0 gio-unix-2.0`
}

package_dde-workspace() {
  pkgdesc="Linux Deepin desktop environment."
  depends=('dde-control-center' 'deepin-menu' 'deepin-artwork'
           'libdeepin-webkit' 'cairo' 'dbus-glib' 'python2')
  optdepends=('lightdm-deepin-greeter')
  replaces=("deepin-desktop-environment" "deepin-workspace")
  install="${pkgbase}.install"

  cd "${pkgbase}-${pkgver}"/build

  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}"/{etc/{X11,sysctl.d},usr/{bin,share/xgreeters}}

  install -m644 ../debian/30-deepin-inotify-limit.conf "${pkgdir}"/etc/sysctl.d/
  install -m755 ../debian/default-terminal "${pkgdir}"/usr/bin/
  install -m644 "${srcdir}"/default-display-manager "${pkgdir}"/etc/X11/

  ## move greeter files to tmp dir
  mkdir -p ${srcdir}/greeter
  mv -v ${pkgdir}/usr/bin/lightdm-deepin-greeter ${srcdir}/greeter
  mv -v ${pkgdir}/usr/share/dde/resources/greeter ${srcdir}/greeter
  mv -v ${pkgdir}/usr/share/personalization/greeter-theme ${srcdir}/greeter
  mv -v ${pkgdir}/usr/share/xgreeters/lightdm-deepin-greeter.desktop ${srcdir}/greeter
}

package_lightdm-deepin-greeter() {
  pkgdesc="Lightdm greeter for Linux Deepin desktop environment."
  depends=('lightdm')

  install -dm755 ${pkgdir}/usr/{bin,share/{dde/resources,personalization,xgreeters}}
  install -dm755 ${pkgdir}/var/lib/polkit-1/localauthority/50-local.d

  install -m755 "${pkgbase}-${pkgver}"/debian/lightdm.pkla "${pkgdir}"/var/lib/polkit-1/localauthority/50-local.d/
  chmod 700 "${pkgdir}"/var/lib/polkit-1

  install -m755 ${srcdir}/greeter/lightdm-deepin-greeter ${pkgdir}/usr/bin/
  install -m755 ${srcdir}/greeter/lightdm-deepin-greeter.desktop ${pkgdir}/usr/share/xgreeters/
  mv -v ${srcdir}/greeter/greeter ${pkgdir}/usr/share/dde/resources/
  mv -v ${srcdir}/greeter/greeter-theme ${pkgdir}/usr/share/personalization/
}

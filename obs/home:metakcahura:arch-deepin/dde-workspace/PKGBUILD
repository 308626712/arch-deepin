# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=dde-workspace
pkgname=('dde-workspace' 'lightdm-deepin-greeter')
pkgver=2.1.20150528170616
_srcdirname=deepin-desktop-environment-2.1+20150528170616~45cd9c8d06
pkgrel=3
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
groups=('deepin')
makedepends=('cmake' 'go' 'coffee-script' 'go-gettext-go' 'webkitgtk'
             'libdeepin-webkit' 'lightdm' 'cairo' 'dbus-glib' 'python2')
source=("http://ftp5.gwdg.de/pub/linux/linuxdeepin/packages/pool/main/d/deepin-desktop-environment/deepin-desktop-environment_2.1+20150528170616~45cd9c8d06.tar.gz"
        "default-display-manager"
        "fix-go1.5-template-issue.patch")
sha256sums=('ffcba9a2680b8070778a0fa26ddb1c8b01280bac40850a2687591ac211756267'
            '3667d6cce9a00f0b8c8bc27250b2ceaf421382d3b4e687a5bcea4c64292030df'
            '55025f4c739b87cb71394b1d53fbd43eb79a4dec0a0996cf53585cd91ef6c28b')

prepare() {
  cd "${srcdir}"/${_srcdirname}
  patch -p1 -i ../fix-go1.5-template-issue.patch

  cp -r "${srcdir}/${_srcdirname}" "${srcdir}/${_srcdirname}-greeter"

  cd "${srcdir}"/${_srcdirname}
  sed "/add_subdirectory(greeter)/d" -i app/CMakeLists.txt || die "sed failed"
}

build() {
  cd "${_srcdirname}"

  #export PYTHON=/usr/bin/python2
  mkdir -p build && cd build/
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make

  gcc -o ../debian/default-terminal ../debian/default-terminal.c `pkg-config --libs --cflags glib-2.0 gio-unix-2.0`

  cd "${srcdir}"/${_srcdirname}-greeter

  mkdir -p build && cd build/
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make
}

package_dde-workspace() {
  pkgdesc="Linux Deepin desktop environment."
  depends=('dde-control-center' 'deepin-menu' 'deepin-artwork'
           'libdeepin-webkit' 'cairo' 'dbus-glib' 'python2')
  optdepends=('lightdm-deepin-greeter')
  replaces=("deepin-desktop-environment" "deepin-workspace")
  install="${pkgbase}.install"

  cd "${_srcdirname}"/build

  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}"/{etc/{X11,sysctl.d},usr/share/backgrounds}

  install -m644 ../debian/30-deepin-inotify-limit.conf "${pkgdir}"/etc/sysctl.d/
  install -m755 ../debian/default-terminal "${pkgdir}"/usr/bin/
  install -m644 "${srcdir}"/default-display-manager "${pkgdir}"/etc/X11/

  ## add default background..
  install -m644 ../resources/greeter/images/background/default_background.jpg "${pkgdir}"/usr/share/backgrounds/
}

package_lightdm-deepin-greeter() {
  pkgdesc="Lightdm greeter for Linux Deepin desktop environment."
  depends=('cairo' 'gtk3' 'libdeepin-webkit' 'lightdm' 'nodejs' 'python2')

  cd "${srcdir}"/${_srcdirname}-greeter/build/app/greeter

  make install DESTDIR="${pkgdir}"
  chmod 0700 "${pkgdir}"/var/lib/polkit-1
}

# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=dde-workspace
pkgname=('dde-workspace' 'lightdm-deepin-greeter')
pkgver=20140522.a4e32db
pkgrel=1
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
groups=('deepin')
makedepends=('cmake' 'go' 'coffee-script' 'go-gettext-go' 'webkitgtk'
             'libdeepin-webkit' 'lightdm' 'cairo' 'dbus-glib' 'python2')
source=("${pkgbase}-${pkgver}.tar.xz" 'default-display-manager')
sha256sums=('SKIP' '3667d6cce9a00f0b8c8bc27250b2ceaf421382d3b4e687a5bcea4c64292030df')

prepare() {
  cp -r "${pkgbase}-${pkgver}" "${pkgbase}-${pkgver}-greeter"

  cd "${srcdir}"/${pkgbase}-${pkgver}
  sed "/add_subdirectory(greeter)/d" -i app/CMakeLists.txt || die "sed failed"
}

build() {
  cd "${pkgbase}-${pkgver}"

  #export PYTHON=/usr/bin/python2
  mkdir -p build && cd build/
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make

  gcc -o ../debian/default-terminal ../debian/default-terminal.c `pkg-config --libs --cflags glib-2.0 gio-unix-2.0`

  cd "${srcdir}"/${pkgbase}-${pkgver}-greeter

  mkdir -p build && cd build/
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make
}

package_dde-workspace() {
  pkgdesc="Linux Deepin desktop environment."
  depends=('dde-control-center' 'deepin-menu' 'deepin-artwork'
           'libdeepin-webkit' 'cairo' 'dbus-glib' 'python2')
  optdepends=('lightdm-deepin-greeter')
  replaces=("deepin-desktop-environment" "deepin-workspace")
  install="${pkgbase}.install"

  cd "${pkgbase}-${pkgver}"/build

  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}"/etc/{X11,sysctl.d}

  install -m644 ../debian/30-deepin-inotify-limit.conf "${pkgdir}"/etc/sysctl.d/
  install -m755 ../debian/default-terminal "${pkgdir}"/usr/bin/
  install -m644 "${srcdir}"/default-display-manager "${pkgdir}"/etc/X11/
}

package_lightdm-deepin-greeter() {
  pkgdesc="Lightdm greeter for Linux Deepin desktop environment."
  depends=('cairo' 'gtk3' 'libdeepin-webkit' 'lightdm' 'nodejs' 'python2')

  cd "${srcdir}"/${pkgbase}-${pkgver}-greeter/build/app/greeter

  make install DESTDIR="${pkgdir}"
}

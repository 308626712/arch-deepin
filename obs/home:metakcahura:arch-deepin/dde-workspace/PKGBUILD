# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=dde-workspace
pkgname=('dde-workspace' 'lightdm-deepin-greeter')
pkgver=2.90.1
pkgrel=1
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
makedepends=('cmake' 'coffee-script' 'dbus-glib' 'git' 'go'
             'libdeepin-webkit' 'lightdm' 'python2' 'webkitgtk')
#source=("git+https://github.com/linuxdeepin/dde-workspace.git#tag=$pkgver"
#		 "go-gettext-go::git+https://github.com/chai2010/gettext-go.git"
#		 'default-display-manager'
#		 'fix-go1.5-template-issue.patch'
#		 '30-deepin-inotify-limit.conf')
source=('dde-workspace.tar.xz'
		'go-gettext-go.tar.xz'
        'default-display-manager'
        'fix-go1.5-template-issue.patch'
        '30-deepin-inotify-limit.conf')
sha256sums=('SKIP'
			'SKIP'
            '3667d6cce9a00f0b8c8bc27250b2ceaf421382d3b4e687a5bcea4c64292030df'
            '55025f4c739b87cb71394b1d53fbd43eb79a4dec0a0996cf53585cd91ef6c28b'
            '57792923fd07d6e99528947735d124e1a6d7a90fbb4535610658b7b5766602ab')

prepare() {
	export GOPATH="$srcdir/build"
	mkdir -p build/src/github.com/chai2010/gettext-go
	cp -a go-gettext-go/* build/src/github.com/chai2010/gettext-go

	cd dde-workspace
	patch -p1 -i ../fix-go1.5-template-issue.patch
	cp -r ../dde-workspace ../dde-workspace-greeter
	sed "/add_subdirectory(greeter)/d" -i app/CMakeLists.txt || die "sed failed"
}

build() {
	export GOPATH="$srcdir/build"
	cd dde-workspace
	mkdir -p build && cd build/
	cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make

	cd ../../dde-workspace-greeter
	mkdir -p build && cd build/
	cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make
}

package_dde-workspace() {
	pkgdesc="Linux Deepin desktop environment."
	groups=('deepin')
	depends=('dbus-glib' 'dde-control-center' 'deepin-artwork' 'deepin-menu'
			 'deepin-nautilus-properties' 'libdeepin-webkit')
	optdepends=('lightdm-deepin-greeter')
	replaces=('deepin-desktop-environment' 'deepin-workspace')
	install='dde-workspace.install'

	cd dde-workspace/build
	make install DESTDIR="${pkgdir}"

	install -dm755 "${pkgdir}"/{etc/{X11,sysctl.d},usr/share/backgrounds}
	install -m644 ../../30-deepin-inotify-limit.conf "${pkgdir}"/etc/sysctl.d/
	install -m644 ../../default-display-manager "${pkgdir}"/etc/X11/
	## add default background..
	install -m644 ../resources/greeter/images/background/default_background.jpg "${pkgdir}"/usr/share/backgrounds/
}

package_lightdm-deepin-greeter() {
	pkgdesc="Lightdm greeter for Linux Deepin desktop environment."
	depends=('libdeepin-webkit' 'lightdm' 'nodejs')

	cd dde-workspace-greeter/build/app/greeter
	make install DESTDIR="${pkgdir}"

	chmod 0700 "${pkgdir}"/var/lib/polkit-1
}

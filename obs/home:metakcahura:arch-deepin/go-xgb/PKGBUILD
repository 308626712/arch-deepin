# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=go-xgb
pkgver=git20140510~eabb7fe
pkgrel=4
pkgdesc='XGB is the X protocol Go language Binding.'
arch=('i686' 'x86_64')
url="https://github.com/BurntSushi/xgb"
license=('GPL2')
depends=('go>=1.3')
makedepends=('xcb-proto')
options=(staticlibs)
source=("xgb-${pkgver}.tar.xz")
sha256sums=('SKIP')

build(){
  cd "${srcdir}"

  mkdir -p go-xgb-build/src/github.com/BurntSushi/
  cp -r xgb-${pkgver} go-xgb-build/src/github.com/BurntSushi/xgb

  # autobuild go
  (cd "${srcdir}"/go-xgb-build/src/github.com/BurntSushi/xgb && make)

  GOPATH="${srcdir}"/go-xgb-build go install github.com/BurntSushi/xgb
  GOPATH="${srcdir}"/go-xgb-build go install github.com/BurntSushi/xgb/{bigreq,composite,damage,dpms,dri2,ge,glx,randr,record,render,screensaver,shape,shm,xcmisc,xevie,xf86dri,xf86vidmode,xfixes,xinerama,xprint,xproto,xselinux,xtest,xv,xvmc}
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib/go/{pkg,src/pkg}
  cp -rf "${srcdir}"/go-xgb-build/src/* "${pkgdir}"/usr/lib/go/src/pkg/
  cp -rf "${srcdir}"/go-xgb-build/pkg/* "${pkgdir}"/usr/lib/go/pkg/
}

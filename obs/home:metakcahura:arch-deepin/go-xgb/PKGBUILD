# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=go-xgb
pkgver=0.0.20140510.eabb7fe
pkgrel=4
pkgdesc='XGB is the X protocol Go language Binding.'
arch=('i686' 'x86_64')
url="https://github.com/BurntSushi/xgb"
license=('GPL2')
depends=('go>=1.3')
makedepends=('xcb-proto')
options=(staticlibs)
source=("${pkgname}-${pkgver}.tar.xz")
sha256sums=('SKIP')

_gobuild_dir="${pkgname}-build"
_gopkg="github.com/BurntSushi/xgb"

prepare() {
  mkdir -p "${_gobuild_dir}"/src/"${_gopkg}"
  cp -r "${pkgname}-${pkgver}"/* "${_gobuild_dir}"/src/"${_gopkg}"/
  rm -rf "${_gobuild_dir}"/src/"${_gopkg}"/debian
}

build() {
  # generate xcb code
  (cd "${srcdir}"/go-xgb-build/src/github.com/BurntSushi/xgb && make)

  GOPATH="${srcdir}/${_gobuild_dir}" go install "${_gopkg}"
  GOPATH="${srcdir}/${_gobuild_dir}" go install "${_gopkg}"/{bigreq,composite,damage,dpms,dri2,ge,glx,randr,record,render,screensaver,shape,shm,xcmisc,xevie,xf86dri,xf86vidmode,xfixes,xinerama,xprint,xproto,xselinux,xtest,xv,xvmc}
}

package() {
  mkdir -p "${pkgdir}"/usr/lib/go/src/pkg/
  cp -r "${_gobuild_dir}"/src/* "${pkgdir}"/usr/lib/go/src/pkg/
}

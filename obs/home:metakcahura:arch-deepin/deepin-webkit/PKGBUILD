# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: 

pkgname=deepin-webkit
_pkgver=1.8.2.1
_pkgvergit=+git20131220153235~9ac8cf628
pkgver=${_pkgver}${_pkgvergit}
pkgrel=3
pkgdesc='A fork of webkit-gtk for Linux Deepin desktop eviroment'
arch=('i686' 'x86_64')
url="http://www.linuxdeepin.com/"
license=('LGPL + GPL + BSD + other')
depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk3' 'gstreamer0.10-base-plugins' 'libsecret' 'libwebp' 'harfbuzz-icu')
makedepends=('gtk2' 'gperf' 'gobject-introspection' 'python2' 'mesa' 'gtk-doc' 'gawk' 'chrpath' 'bison' 'perl' 'flex' 'which' 'gettext')
optdepends=('gtk2: Netscape plugin support')
provides=("${pkgname}")
groups=('deepin')
options=(!libtool !emptydirs)
source=("http://test.packages.linuxdeepin.com/deepin/pool/main/d/${pkgname}/${pkgname}_${pkgver}.tar.gz"
        "webkit-gtk-1.8.2-bison-2.6.patch"
	"DerivedSources.tar.gz")
sha256sums=('7f15b029114d33b994a276d3967cf24a172a532a14d33f3f07a7dd44a8d8758f'
	    'cea36d9a955e4f5c2cd2fe2037ff63067217596e88cbbaa1da2ecc7546d3cd44'
	    '7a5fbc735dcda90faeafb26778a741ebe1a5fcf0cfb44de5369db42aa60561df')

build() {
  cd "${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -Wall -Wl,--as-needed"
  export LDFLAGS="$LDFLAGS -Wl,--no-keep-memory"

  patch -p1 -i "${srcdir}"/webkit-gtk-1.8.2-bison-2.6.patch

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --disable-silent-rules \
      --with-gtk=3.0 \
      --disable-webgl
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
  mv "${pkgdir}"/usr/lib/pkgconfig/webkitgtk-3.0.pc "${pkgdir}"/usr/lib/pkgconfig/deepin-webkit-3.0.pc
}

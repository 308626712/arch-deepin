# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dde-daemon
pkgver=0.0.1.20150619132138
_srcdirname=dde-daemon-0.0.1+20150619132138~b7120bedfa
pkgrel=2
pkgdesc='Go-lang bingdings for dde-daemon'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-daemon"
license=('GPL3')
depends=('dde-api')
makedepends=('go' 'go-dlib' 'dde-go-dbus-factory' 'go-xgb' 'go-gocheck'
             'go-xgbutil' 'go-fsnotify' 'go-sqlite3' 'python2' 'glib2'
             'gtk2' 'gtk3' 'metacity-ubuntu<3.0')
optdepends=('networkmanager: Network Management daemon')
groups=('deepin')
install="${pkgname}.install"
conflicts=('dde-daemon-gsd-input')
source=("http://ftp5.gwdg.de/pub/linux/linuxdeepin/packages/pool/main/d/dde-daemon/dde-daemon_0.0.1+20150619132138~b7120bedfa.tar.gz"
        "fix-i686-compile.patch"
        "fix-block-issue-under-go1.5.patch")
sha256sums=('9ed5c8b1fffa2a724d169990287214dfce56aa1883e0eb9c9d8973b5dbe3a282'
            'f54fdefa940e88e7303bdfd11f2c4c353685947616e5cbb3085b92678df20e69'
            'd60f6c371160ecf9a000f1b99c1579a33a38b8262295db97116c3dd8e782e3fb')

prepare() {
	cd "${_srcdirname}"

	# TODO: a temporary solution by disable datatime module of
	# dde-session-daemon to avoid build issue for go1.4/i686
	if test "$CARCH" == i686; then
	  patch -p1 -i ../fix-i686-compile.patch
	fi

	patch -p1 -i ../fix-block-issue-under-go1.5.patch
}

build() {
	cd "${_srcdirname}"

	make GOPATH="/usr/lib/go/site"
}

package() {
	depends+=('acpid' 'bluez' 'grub-theme-deepin' 'iso-codes' 'lsb-release'
	    'mobile-broadband-provider-info' 'polkit-gnome' 'upower' 'xcur2png')

	cd "${_srcdirname}"

	make DESTDIR="${pkgdir}" install
}

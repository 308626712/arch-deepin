# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-nautilus-properties
pkgver=3.14.2
pkgrel=1
pkgdesc="Provide file property dialog for Deepin desktop environment"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'libnautilus-extension')
makedepends=('intltool' 'gobject-introspection' 'libnotify')
url="https://gitcafe.com/Deepin/deepin-nautilus-properties"
options=('!emptydirs')
# source=("git+https://github.com/linuxdeepin/deepin-nautilus-properties.git#tag=${pkgver}")
source=("deepin-nautilus-properties.tar.xz")
sha256sums=('SKIP')

build() {
  cd deepin-nautilus-properties

  # regenerate autotools files
  libtoolize && aclocal && autoheader && automake --add-missing && autoconf

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib/nautilus \
              --disable-nst-extension \
              --disable-update-mimedb \
              --disable-packagekit \
              --disable-introspection \
              --disable-tracker
  make
}

package() {
  cd deepin-nautilus-properties

  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  rm -rf "${pkgdir}"/usr/{etc,include,lib,share}
  rm -f "${pkgdir}"/usr/bin/{nautilus,nautilus-autorun-software,nautilus-connect-server}
}

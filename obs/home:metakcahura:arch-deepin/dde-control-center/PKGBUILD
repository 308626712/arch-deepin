# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dde-control-center
pkgver=git20140526~951e4cc
pkgrel=1
pkgdesc="New control center for linux deepin"
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-control-center"
license=('GPL3')
depends=('dde-daemon' 'deepin-qml-widgets' 'dde-qml-dbus-factory'
		 'qml-dde-dock-applet' 'qt5-svg' 'qt5-declarative'
		 'qt5-graphicaleffects' 'qt5-tools' 'qt5-quickcontrols')
makedepends=('python2')
groups=('deepin')
options=(staticlibs)
source=("${pkgname}-${pkgver}.tar.xz" 'locale.patch')
sha256sums=('SKIP' 'SKIP')

prepare() {
	cd "${pkgname}-${pkgver}"

	patch -p0 -i ../locale.patch

	# fix python version
	find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build(){
	cd "${pkgname}-${pkgver}"

	make
}

package() {
	depends+=('deepin-artwork-themes' 'dde-account-faces')

	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}

# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dde-control-center
pkgver=14.12.20150902104832
_srcdirname=dde-control-center-14.12+git20150902104832~64f57d1aaf
pkgrel=3
pkgdesc="New control center for linux deepin"
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-control-center"
license=('GPL3')
depends=('qt5-svg' 'qt5-declarative' 'qt5-quickcontrols')
makedepends=('deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
source=("http://ftp5.gwdg.de/pub/linux/linuxdeepin/packages/pool/main/d/dde-control-center/dde-control-center_14.12+git20150902104832~64f57d1aaf.tar.gz")
sha256sums=('ac86a85f4c829de49a12e5ff68195fd385f64f692c17d60b2d14bbd759490121')

prepare() {
	cd "${_srcdirname}"

	# fix python version
	find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build(){
	cd "${_srcdirname}"

	make
}

package() {
	depends+=('deepin-artwork-themes' 'dde-account-faces' 'dde-daemon'
			  'deepin-qml-widgets' 'dde-qml-dbus-factory' 'dde-api')

	cd "${_srcdirname}"

	make DESTDIR="${pkgdir}" install
}

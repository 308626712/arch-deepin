# $Id$
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.12.0
pkgrel=1
pkgdesc="Legacy GNOME window manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'gsettings-desktop-schemas' 'gtk2' 'startup-notification' 'zenity')
makedepends=('gettext' 'intltool' 'itstool')
url="https://wiki.gnome.org/Projects/Metacity"
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        stop-spamming-xsession-errors.patch
        Dont-focus-ancestor-window-on-a-different-workspac.patch
        metacity-2.28-empty-keybindings.patch)
sha256sums=('a9ac5a3decd14fe6068efb5b5358bb4dffd0380ce2d605cb65424d843c3ed9dd'
            'f675125a9543e4d65974d8965ca34471d95d22d0d08341abbbf40f5b366cab94'
            'cd21b750e59fcadf39e5fb344b0e0c57da13ba45da86e62ed093a5d4b950d852'
            '94ff740ec5fce00103ba42ca74380f12549e8da5e83a3a5955ffe628010e789f')

prepare() {
  cd "$pkgname-$pkgver"

  # http://bugzilla.gnome.org/show_bug.cgi?id=558723
  patch -Np1 -i "$srcdir/stop-spamming-xsession-errors.patch"

  # https://bugzilla.gnome.org/show_bug.cgi?id=598995
  patch -Np1 -i "$srcdir/Dont-focus-ancestor-window-on-a-different-workspac.patch"

  # https://bugzilla.gnome.org/show_bug.cgi?id=559816
  patch -Np1 -i "$srcdir/metacity-2.28-empty-keybindings.patch"
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=deepin-desktop-environment
pkgver=git20140522~a4e32db
pkgrel=1
pkgdesc="Linux Deepin desktop environment."
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
depends=('dde-control-center' 'deepin-menu' 'deepin-artwork'
	 'deepin-webkit' 'cairo' 'dbus-glib' 'python2')
makedepends=('cmake' 'go>=1.3' 'coffee-script' 'golang-gettext-go' 'gettext' 'webkitgtk' 'lightdm')
optdepends=('lightdm: LightDM for lightdm-deepin-greeter')
groups=('deepin')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.xz" 'default-display-manager')
sha256sums=('SKIP' '3667d6cce9a00f0b8c8bc27250b2ceaf421382d3b4e687a5bcea4c64292030df')

build() {
  cd "${pkgname}-${pkgver}"

  [[ -d build ]] && rm -rvf build/
  mkdir build && cd build

  #export PYTHON=/usr/bin/python2
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make

  gcc -o ../debian/default-terminal ../debian/default-terminal.c `pkg-config --libs --cflags glib-2.0 gio-unix-2.0`
}

package() {
  cd "${pkgname}-${pkgver}"/build

  make install DESTDIR="${pkgdir}/"

  install -dm 755 "${pkgdir}"/{etc/{X11,sysctl.d},usr/{bin,share/xgreeters},var/lib/polkit-1/localauthority/50-local.d}

  install -m 644 ../debian/30-deepin-inotify-limit.conf "${pkgdir}"/etc/sysctl.d/
  install -m 644 ../debian/lightdm-deepin-greeter.desktop "${pkgdir}"/usr/share/xgreeters/
  install -m 755 ../debian/default-terminal "${pkgdir}"/usr/bin/
  install -m 755 ../debian/lightdm.pkla "${pkgdir}"/var/lib/polkit-1/localauthority/50-local.d/
  install -m 644 "${srcdir}"/default-display-manager "${pkgdir}"/etc/X11/
  chmod 700 "${pkgdir}"/var/lib/polkit-1
}

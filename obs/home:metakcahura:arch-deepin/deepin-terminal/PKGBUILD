# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: 

pkgname=deepin-terminal
pkgver=1.1.git20140515~a4c7a46
pkgrel=1
pkgdesc='Awesome terminal for Linux Deepin'
arch=('any')
url="https://gitcafe.com/Deepin/deepin-terminal"
license=('GPL3')
depends=('deepin-ui' 'deepin-vte-plus' 'gtk-update-icon-cache' 'python2-dbus' 'python2-scipy')
groups=('deepin')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.xz" 'arch-makefile-fix.patch')
sha256sums=('SKIP' '87d858fb601a708854ffa04198d63c2a802bf4ad57f43d62201fd0aa59ed29cd')

prepare(){
  cd "${pkgname}-${pkgver}"
  
  patch -i "${srcdir}/arch-makefile-fix.patch"
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  # fix python version
  find "${pkgdir}" -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python=\1python2='

  # there is no 'XHei' font on archinux, so replace it
  sed -i 's/XHei Mono.Ubuntu/Monospace/' "${pkgdir}"/usr/share/${pkgname}/src/main.py
}

# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-movie
pkgver=git20140528~08a5dde
pkgrel=1
pkgdesc='deepin movie for linuxdeepin desktop environment'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
makedepends=('python2-sip')
groups=('deepin-extra')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.xz" 'generate_mo.sh')
sha256sums=('SKIP' '89eb6ed5337d7e9e5c77107bf5d763cf1f6aeb429233420e7f67072afe1d2f6e')

prepare() {
  cd "${pkgname}-${pkgver}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${pkgname}-${pkgver}"

  cd locale && sh "${srcdir}"/generate_mo.sh
  #make
}

package() {
  depends=('qt5-declarative' 'qt5-quickcontrols' 'qt5-multimedia' 'qt5-webkit'
           'qt5-graphicaleffects' 'qt5-imageformats' 'qt5-systems' 'python2-pyqt5'
           'mediainfo' 'python2-ass' 'deepin-menu' 'deepin-qml-widgets' 'enca'
           'python2-pysrt' 'python2-setproctitle' 'dde-go-dbus-factory'
           'deepin-ui' 'desktop-file-utils' 'python2-magic' python2-xpybutil)

  cd "${pkgname}-${pkgver}"

  #make DESTDIR="${pkgdir}" PREFIX="/usr" install

  install -dm755 "${pkgdir}"/usr/{bin,share/{applications,deepin-movie,icons/hicolor/scalable/apps,locale}}

  install -m 644 *.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 image/deepin-movie.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/

  mv -v *.py "${pkgdir}"/usr/share/deepin-movie/
  mv -v *.qml "${pkgdir}"/usr/share/deepin-movie/
  mv -v image "${pkgdir}"/usr/share/deepin-movie/
  mv -v locale/mo/* "${pkgdir}"/usr/share/locale/

  ln -s /usr/share/deepin-movie/main.py "$pkgdir"/usr/bin/deepin-movie
}

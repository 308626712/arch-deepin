# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

_pkgname=golang-gettext-go
pkgname=go-gettext-go
_pkgverbase=0.0
_pkgvergit=20140603
_pkgver=${_pkgverbase}~hg${_pkgvergit}
pkgver=${_pkgverbase}.${_pkgvergit}
pkgrel=3
pkgdesc="This package provides GNU gettext support."
arch=('i686' 'x86_64')
url="https://code.google.com/p/gettext-go/"
license=('GPL2')
depends=('go>=1.3')
replaces=('golang-gettext-go')
options=(staticlibs)
source=("http://packages.linuxdeepin.com/deepin/pool/main/g/${_pkgname}/${_pkgname}_${_pkgver}.orig.tar.xz")
sha256sums=('768a014e1d1e65ae67290a5c66703a3ed99870ab6971f34065eb8c2220a30b9e')

_gobuild_dir="${_pkgname}-build"
_gopkg="code.google.com/p/gettext-go/gettext"

prepare() {
  mkdir -p "${_gobuild_dir}"/src/"${_gopkg}"
  cp -r gettext-go/gettext/* "${_gobuild_dir}"/src/"${_gopkg}"/
}

build() {
  GOPATH="${srcdir}/${_gobuild_dir}" go install "${_gopkg}"
  GOPATH="${srcdir}/${_gobuild_dir}" go install "${_gopkg}"/{mo,plural,po}
}

package() {
  mkdir -p "${pkgdir}"/usr/lib/go/src/pkg/
  cp -r "${_gobuild_dir}"/src/* "${pkgdir}"/usr/lib/go/src/pkg/
}

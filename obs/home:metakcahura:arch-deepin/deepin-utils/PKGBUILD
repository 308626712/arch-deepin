# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=python2-deepin-utils
_pkgname=deepin-utils
pkgver=0.0.2.git20140509~8aaf2a6
pkgrel=1
pkgdesc='Basic utils for all project in Linux Deepin.'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/deepin-utils"
depends=('deepin-pygtk' 'python2-cairo' 'freetype2' 'webkitgtk2' 'python2-xlib' 'pywebkitgtk')
makedepends=('python2-setuptools')
license=('GPL3')
groups=('deepin')
provides=('deepin-utils')
source=("${_pkgname}-${pkgver}.tar.xz")
sha256sums=('SKIP')

prepare() {
  cd "${_pkgname}-${pkgver}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python=\1python2='
}

build() {
  cd "${_pkgname}-${pkgver}"
  
  python2 setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  
  python2 setup.py install --root="$pkgdir/" --optimize=1 
}

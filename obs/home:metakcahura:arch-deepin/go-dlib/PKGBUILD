# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=go-dlib
pkgver=0.0.4.20140522.3948504
pkgrel=1
pkgdesc='Go-lang bingdings for dlib'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/go-lib"
license=('GPL3')
makedepends=('go' 'go-gocheck' 'go-xgb' 'go-fsnotify'
			 'gtk3' 'gdk-pixbuf2' 'libpulse' 'glib2')
groups=('deepin-dev')
options=(staticlibs)
source=("${pkgname}-${pkgver}.tar.xz")
sha256sums=('SKIP')

_gobuild_dir="${pkgname}-build"
_gopkg="pkg.linuxdeepin.com/lib"

prepare() {
  mkdir -p "${_gobuild_dir}"/src/"${_gopkg}"
  cp -r "${pkgname}-${pkgver}"/* "${_gobuild_dir}"/src/"${_gopkg}"/
  rm -rf "${_gobuild_dir}"/src/"${_gopkg}"/debian
}

# go-dlib depends on other go library, and do not try to run
# `go install' here or will appear permission denied problem

package() {
  mkdir -p "${pkgdir}"/usr/lib/go/src
  cp -r "${_gobuild_dir}"/src/* "${pkgdir}"/usr/lib/go/src
  find "${pkgdir}"/usr/lib/go/src -type d -name 'testdata' | xargs rm -rf
}

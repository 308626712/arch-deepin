# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=nodejs
pkgver=0.11.9.105.g055f7e9
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl')
makedepends=('python2' 'procps-ng' 'which')
provides=('nodejs-node-gyp')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.xz")
sha256sums=('SKIP')

prepare() {
	cd "${pkgname}-${pkgver}"

	msg 'Fixing for python2 name'
	find -type f -exec sed \
	-e 's_^#!/usr/bin/env python$_&2_' \
	-e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
	-e 's_^#!/usr/bin/python$_&2_' \
	-e "s_'python'_'python2'_" -i {} \;
	find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
	cd "${pkgname}-${pkgver}"

	export PYTHON=python2
	./configure \
		--prefix=/usr \
		--shared-openssl

	make -j8
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

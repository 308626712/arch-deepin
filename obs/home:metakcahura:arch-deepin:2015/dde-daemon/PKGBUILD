# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dde-daemon
pkgver=0.0.1.20150415132138
pkgrel=1
pkgdesc='daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs'
		 'libcanberra-pulse' 'metacity' 'poppler-glib' 'rfkill')
makedepends=('dde-api' 'go-packages' 'gtk2' 'python2' 'sqlite3' 'startdde')
optdepends=('networkmanager: Network Management daemon'
			'grub-theme-deepin: deepin theme for grub menu')
groups=('deepin')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.xz"
        'fix-i686-compile.patch')
sha256sums=('SKIP'
            '7b147d9ab9c29dcfa73906aa743d03aae7bed317553e42c604d3c1bf539ba1bc')

prepare() {
	cd "${pkgname}-${pkgver}"
	# TODO: a temporary solution by disable datatime module of
	# dde-session-daemon to avoid build issue for go1.4/i686
	[[ $CARCH == "i686" ]] && patch -p1 -i ../fix-i686-compile.patch
}

build() {
	cd "${pkgname}-${pkgver}"
	make GOPATH="/usr/lib/go"
}

package() {
	depends+=('acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
			  'mobile-broadband-provider-info' 'polkit-gnome'
			  'udisks2' 'upower' 'xcur2png')
	cd "${pkgname}-${pkgver}"
	make GOPATH="/usr/lib/go" DESTDIR="${pkgdir}" install
}

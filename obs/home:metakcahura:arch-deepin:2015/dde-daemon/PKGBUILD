# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dde-daemon
pkgver=2.93.0
_commit=6726ab7281f17fe2878f6b035f671be9f1f16ccb
pkgrel=2
pkgdesc='daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs'
		 'libcanberra-pulse' 'metacity' 'poppler-glib' 'rfkill')
makedepends=('dde-api' 'go-packages' 'sqlite3')
optdepends=('networkmanager: Network Management daemon'
			'grub-theme-deepin: deepin theme for grub menu')
groups=('deepin')
install="${pkgname}.install"
# source=("git+https://github.com/linuxdeepin/dde-daemon.git#commit=$_commit"
#         'fix-i686-compile.patch')
source=("dde-daemon.tar.xz"
        'fix-i686-compile.patch')
sha256sums=('SKIP'
            '1dccf88c5ce480560a4a2d73134e69f05135703fe34ccd5d9e2e5d7fe852efc5')

prepare() {
	cd dde-daemon
	# TODO: a temporary solution by disable go-sqlite3 to avoid build
	#       issue for go1.4/i686
    # https://github.com/golang/go/issues/9510
	if [[ $CARCH == "i686" ]]; then
        patch -p1 -i ../fix-i686-compile.patch
    fi
}

build() {
	cd dde-daemon
	make GOPATH="/usr/lib/go"
}

package() {
	depends+=('acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
			  'mobile-broadband-provider-info' 'polkit-gnome'
			  'udisks2' 'upower' 'xcur2png')
	cd dde-daemon
	make GOPATH="/usr/lib/go" DESTDIR="${pkgdir}" install
}

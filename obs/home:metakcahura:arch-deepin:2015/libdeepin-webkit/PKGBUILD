# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=libdeepin-webkit
pkgver=1.8.2.2.20141124
pkgrel=1
pkgdesc='A fork of webkit-gtk for Linux Deepin desktop eviroment'
arch=('i686' 'x86_64')
url="http://www.linuxdeepin.com/"
license=('LGPL' 'GPL' 'BSD' 'custom')
depends=('geoclue' 'libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant'
         'libgl' 'gtk3' 'gstreamer0.10-base-plugins' 'libsecret'
         'libwebp' 'harfbuzz-icu')
makedepends=('gtk2' 'gperf' 'gobject-introspection' 'python2' 'mesa'
             'gtk-doc' 'gawk' 'chrpath' 'bison' 'perl' 'flex')
optdepends=('gtk2: Netscape plugin support')
options=(!libtool !emptydirs)
source=("${pkgname}-${pkgver}.tar.xz"
        "webkit-gtk-1.8.2-bison-2.6.patch")
sha256sums=('SKIP'
            'cea36d9a955e4f5c2cd2fe2037ff63067217596e88cbbaa1da2ecc7546d3cd44')

prepare() {
	cd "${pkgname}-${pkgver}"

	patch -p1 -i ../webkit-gtk-1.8.2-bison-2.6.patch
}

build() {
	cd "${pkgname}-${pkgver}"

	export CFLAGS="$CFLAGS -Wall -Wl,--as-needed"
	export LDFLAGS="$LDFLAGS -Wl,--no-keep-memory"

	PYTHON=/usr/bin/python2 ./configure \
	    --prefix=/usr \
	    --disable-silent-rules \
	    --with-gtk=3.0 \
	    --disable-webgl
	make -j8
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="$pkgdir" install

	rm -rf "$pkgdir"/usr/{bin,include,share}
	mv "${pkgdir}"/usr/lib/pkgconfig/webkitgtk-3.0.pc "${pkgdir}"/usr/lib/pkgconfig/deepin-webkit-3.0.pc
}

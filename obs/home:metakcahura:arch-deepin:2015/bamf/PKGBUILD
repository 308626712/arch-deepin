# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=bamf
pkgver=r613
pkgrel=1
pkgdesc='Application matching framework'
arch=('i686' 'x86_64')
url='https://launchpad.net/bamf'
license=('GPL')
depends=('dbus-glib' 'libgtop' 'libwnck3' 'python2-lxml')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
source=("${pkgname}-${pkgver}.tar.xz")
sha256sums=('SKIP')

prepare() {
	cd "${pkgname}-${pkgver}"

	sed -i 's|^#!.*python$|#!/usr/bin/python2|' tests/gtester2xunit.py
}

build() {
	cd "${pkgname}-${pkgver}"

	export PYTHON=/usr/bin/python2

	./autogen.sh \
		--prefix='/usr' \
		--localstatedir='/var' \
		--libexecdir='/usr/lib/bamf' \
		--sysconfdir='/etc' \
		--disable-static
	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}

# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dde-workspace
pkgver=2.1.20150521160615
pkgrel=2
pkgdesc="Linux Deepin desktop environment."
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-workspace"
license=('GPL3')
depends=('dbus-glib' 'dde-control-center' 'deepin-desktop-schemas'
		 'lightdm' 'libdeepin-webkit')
makedepends=('cmake' 'coffee-script' 'go-packages' 'python2' 'webkitgtk')
groups=('deepin')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.xz"
		'30-deepin-inotify-limit.conf')
sha256sums=('SKIP'
			'57792923fd07d6e99528947735d124e1a6d7a90fbb4535610658b7b5766602ab')

build() {
	cd "${pkgname}-${pkgver}"
	mkdir -p build && cd build/
	cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release && make
}

package() {
	depends+=('deepin-menu')
	cd "${pkgname}-${pkgver}/build"
	make install DESTDIR="${pkgdir}"
	install -dm755 "${pkgdir}"/{etc/sysctl.d,usr/share/backgrounds}
	install -m644 "${srcdir}"/30-deepin-inotify-limit.conf "${pkgdir}"/etc/sysctl.d/
	## add default background..
	install -m644 ../resources/greeter/images/background/default_background.jpg "${pkgdir}"/usr/share/backgrounds/
}

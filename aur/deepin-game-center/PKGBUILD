# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=deepin-game-center
pkgver=1.0.1.20140912162350
pkgrel=1
pkgdesc="Game center for Linuxdeepin"
arch=('any')
url="https://github.com/linuxdeepin/deepin-game-center"
license=('GPL2')
depends=('deepin-ui' 'flashplugin' 'hicolor-icon-theme' 'python2' 'python2-scipy' 'python2-jswebkit')
install=${pkgname}.install

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-game-center/deepin-game-center_1.0.1+20140912162350~e5f86f085a.tar.gz"
source=("${_fileurl}")
sha256sums=('498b1b39b6d6e7a798ceebe7a91ca772839ac50fccd594ce782947047fbf803d')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

prepare() {
    cd "${srcdir}/${_innerdir}"

    # fix python version
    find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

    # TODO remove pycompile
    sed -i '/pycompile/d' Makefile
}

build() {
    cd "${srcdir}/${_innerdir}"

    make
}

package() {
    cd "${srcdir}/${_innerdir}"

    make DESTDIR="${pkgdir}" PREFIX="/usr" install
}

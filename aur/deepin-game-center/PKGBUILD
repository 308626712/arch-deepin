# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=deepin-game-center
pkgver=1.0.1.20141029123213
pkgrel=1
pkgdesc="Game center for Linuxdeepin"
arch=('any')
url="https://github.com/linuxdeepin/deepin-game-center"
license=('GPL2')
depends=('deepin-ui' 'flashplugin' 'hicolor-icon-theme' 'python2' 'python2-scipy' 'python2-jswebkit')
install=${pkgname}.install

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-game-center/deepin-game-center_1.0.1+20141029123213~76026badac.tar.gz"
source=("${_fileurl}")
sha256sums=('73e9dc8a508a562fa913c4bfa60d167efd7f47fe933a6c014973a310c333a0ae')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

prepare() {
    cd "${srcdir}/${_innerdir}"

    # fix python version
    find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

    # TODO remove pycompile
    sed -i '/pycompile/d' Makefile
}

build() {
    cd "${srcdir}/${_innerdir}"

    make
}

package() {
    cd "${srcdir}/${_innerdir}"

    make DESTDIR="${pkgdir}" PREFIX="/usr" install
}

# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>
# Contributor: 4679kun <admin[AT]4679.us>
# Contributor: dongfengweixiao <dongfengweixiao[AT]gmail.com>
# Contributor: flu

pkgname=deepin-ui
pkgver=1.1.20141029135629
pkgrel=1
pkgdesc='UI toolkit for Linux Deepin'
arch=('i686' 'x86_64')
url="http://www.linuxdeepin.com/"
license=('GPL3')
depends=('deepin-utils' 'deepin-gsettings' 'deepin-pygtk' 'python2-distribute' 'python2' 'glib2' 'python2-cairo' 'python2-imaging' 'libwebkit' 'python2-xlib' 'pywebkitgtk' 'libsoup')

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-ui/deepin-ui_1.1+20141029135629~b38fc63e4b.tar.gz"
source=("${_fileurl}")
sha256sums=('f34fed040892c630d962425a7c582a5616b8529a5ae483146e88e3a2256cbb8a')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

prepare() {
  cd "${srcdir}/${_innerdir}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${srcdir}/${_innerdir}"

  python2 setup.py build
  (cd dtk/tools; python2 generate_mo.py)
}

package() {
  cd "${srcdir}/${_innerdir}"

  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -dm 755 "${pkgdir}"/usr/share/locale
  cp -r dtk/locale/mo/* "${pkgdir}"/usr/share/locale/
}

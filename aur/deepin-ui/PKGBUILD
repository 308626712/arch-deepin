# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>
# Contributor: 4679kun <admin[AT]4679.us>
# Contributor: dongfengweixiao <dongfengweixiao[AT]gmail.com>
# Contributor: flu

pkgname=deepin-ui
pkgver=1.1.20140929181647
pkgrel=1
pkgdesc='UI toolkit for Linux Deepin'
arch=('i686' 'x86_64')
url="http://www.linuxdeepin.com/"
license=('GPL3')
depends=('deepin-utils' 'deepin-gsettings' 'deepin-pygtk' 'python2-distribute' 'python2' 'glib2' 'python2-cairo' 'python2-imaging' 'libwebkit' 'python2-xlib' 'pywebkitgtk' 'libsoup')

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-ui/deepin-ui_1.1+20140929181647~7fb9d7dbf5.tar.gz"
source=("${_fileurl}")
sha256sums=('2f2fc9c15a25d836cedacd45500bfda49fc29ecd9c480313d9d595a581c84742')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

prepare() {
  cd "${srcdir}/${_innerdir}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${srcdir}/${_innerdir}"

  python2 setup.py build
  (cd dtk/tools; python2 generate_mo.py)
}

package() {
  cd "${srcdir}/${_innerdir}"

  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -dm 755 "${pkgdir}"/usr/share/locale
  cp -r dtk/locale/mo/* "${pkgdir}"/usr/share/locale/
}

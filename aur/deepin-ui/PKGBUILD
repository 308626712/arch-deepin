# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>
# Contributor: 4679kun <admin[AT]4679.us>
# Contributor: dongfengweixiao <dongfengweixiao[AT]gmail.com>
# Contributor: flu

pkgname=deepin-ui
pkgver=1.1.20140827144926
pkgrel=1
pkgdesc='UI toolkit for Linux Deepin'
arch=('i686' 'x86_64')
url="http://www.linuxdeepin.com/"
license=('GPL3')
depends=('deepin-utils' 'deepin-gsettings' 'deepin-pygtk' 'python2-distribute' 'python2' 'glib2' 'python2-cairo' 'python2-imaging' 'libwebkit' 'python2-xlib' 'pywebkitgtk' 'libsoup')

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-ui/deepin-ui_1.1+20140827144926~f4aa1e43f8.tar.gz"
source=("${_fileurl}")
sha256sums=('395f195853fb9e724ad09e96214dea4b677559c2e8f9b6f05e9c15a5d36144e7')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

build(){
    cd "${srcdir}/${_innerdir}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${_innerdir}"
    mkdir -p "${pkgdir}"/usr/share/locale
    mkdir -p "${pkgdir}"/usr/lib/python2.7/site-packages/dtk/theme
    python2 setup.py install --root="${pkgdir}/" --optimize=1
    mv "${pkgdir}"/usr/dtk/locale "${pkgdir}"/usr/share/
    mv "${pkgdir}"/usr/dtk/theme "${pkgdir}"/usr/lib/python2.7/site-packages/dtk/
    rm -rf "${pkgdir}"/usr/dtk/

    # fix python version
    find "${pkgdir}" -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

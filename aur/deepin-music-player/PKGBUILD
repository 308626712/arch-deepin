# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>
# Contributor: 4679kun <admin[AT]4679.us>
# Contributor: dongfengweixiao <dongfengweixiao[AT]gmail.com>

pkgname=deepin-music-player
pkgver=1.1.20140929174751
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based, gstreamer front-end, with features likes search music by pinyin,quanpin, colorful lyrics supports, and more powerfull functions you will found.'
arch=('any')
url="http://www.linuxdeepin.com/"
license=('GPL3')
depends=('gstreamer0.10-python' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-good-plugins' 'gstreamer0.10-ugly-plugins' 'mutagen' 'python2-chardet' 'python2-scipy' 'python2-pyquery' 'python2-cssselect' 'deepin-ui' 'python2-dbus' 'sonata' 'cddb-py' 'python2-pycurl' 'python2-xlib' 'python2-keybinder2')

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-music-player/deepin-music-player_1.1+20140929174751~8731638598.tar.gz"
source=("${_fileurl}")
sha256sums=('f14c5be810bc14c8a5d692994fa31a375ebc8cf2436617fac180f6d838681f9f')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

prepare() {
    cd "${srcdir}/${_innerdir}"

    # fix python version
    find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

    # TODO remove pycompile
    sed -i '/pycompile/d' Makefile
}

build() {
    cd "${srcdir}/${_innerdir}"

    make
}

package() {
    cd "${srcdir}/${_innerdir}"

    make DESTDIR="${pkgdir}" PREFIX="/usr" install
}

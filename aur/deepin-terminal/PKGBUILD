# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=1.1.20140826163037
pkgrel=1
pkgdesc='Awesome terminal for Linux Deepin'
arch=('any')
url="http://www.linuxdeepin.com/"
license=('GPL2')
depends=('deepin-ui' 'deepin-vte-plus' 'python2-dbus')

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-terminal/deepin-terminal_1.1+20140826163037~afb36faadf.tar.gz"
source=("${_fileurl}")
sha256sums=('038c234bd012793e37438fb7dacd85850c855b40095b933951dfe0663953cbdb')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

prepare() {
    cd "${srcdir}/${_innerdir}"

    # fix python version
    find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

    # TODO remove pycompile
    sed -i '/pycompile/d' Makefile
}

build() {
    cd "${srcdir}/${_innerdir}"

    make
}

package() {
    cd "${srcdir}/${_innerdir}"

    make DESTDIR="${pkgdir}" PREFIX="/usr" install
}

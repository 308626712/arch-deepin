# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=1.1.20140912164758
pkgrel=1
pkgdesc='Awesome terminal for Linux Deepin'
arch=('any')
url="http://www.linuxdeepin.com/"
license=('GPL2')
depends=('deepin-ui' 'deepin-vte-plus' 'python2-dbus')

_fileurl="http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-terminal/deepin-terminal_1.1+20140912164758~7d3c528b69.tar.gz"
source=("${_fileurl}")
sha256sums=('01deedce64242a10dfdc8f161a6598e35267c1cd2f5055667f5760c4e3807999')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

prepare() {
    cd "${srcdir}/${_innerdir}"

    # fix python version
    find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

    # TODO remove pycompile
    sed -i '/pycompile/d' Makefile
}

build() {
    cd "${srcdir}/${_innerdir}"

    make
}

package() {
    cd "${srcdir}/${_innerdir}"

    make DESTDIR="${pkgdir}" PREFIX="/usr" install
}

# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screenshot
pkgver=git20140306151055
pkgrel=1
pkgdesc="Provide a quite easy-to-use screenshot tool. Features:Global hotkey to triggle screenshot tool,Take screenshot of a selected area,Easy to add text and line drawings onto the screenshot"
arch=('any')
url="http://www.linuxdeepin.com/"
license=('LGPL3')
depends=('python2' 'gconf' 'python2-xlib' 'deepin-ui' 'python2-wnck' 'python2-xdg' 'python2-scipy' 'python2-pycurl')

_fileurl=http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-screenshot/deepin-screenshot_2.0+git20140306151055~efdef9b724.tar.gz
source=("${_fileurl}")
md5sums=('d4b5b06fcd56dc50445d02cfb30abdd7')

_filename="$(basename "${_fileurl}")"
_filename="${_filename%.tar.gz}"
_innerdir="${_filename/_/-}"

_install_copyright_and_changelog() {
    mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
    cp -f debian/copyright "${pkgdir}/usr/share/doc/${pkgname}/"
    gzip -c debian/changelog > "${pkgdir}/usr/share/doc/${pkgname}/changelog.gz"
}

# Usage: _easycp dest files...
_easycp () {
    local dest=$1; shift
    mkdir -p "${dest}"
    cp -R -t "${dest}" "$@"
}

prepare() {
    cd "${srcdir}/${_innerdir}"

    # fix python version
    find "${srcdir}" -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python=\1python2='
}

package() {
    cd "${srcdir}/${_innerdir}"

    _easycp "${pkgdir}"/usr/share/deepin-screenshot/ src
    _easycp "${pkgdir}"/usr/share/deepin-screenshot/ theme
    _easycp "${pkgdir}"/usr/share/deepin-screenshot/ skin
    _easycp "${pkgdir}"/usr/share/ locale
    _easycp "${pkgdir}"/usr/share/icons/hicolor/48x48/apps/ debian/deepin-screenshot.png

    mkdir -p "${pkgdir}"/usr/share/applications/
    install -m 0644 debian/deepin-screenshot.desktop "${pkgdir}"/usr/share/applications/

    mkdir -p "${pkgdir}"/usr/bin
    ln -s /usr/share/deepin-screenshot/src/screenshot.py "${pkgdir}"/usr/bin/deepin-screenshot

    _install_copyright_and_changelog

    # remove .po
    find "${pkgdir}" -name '*.po' -exec rm \{} \;
    find "${pkgdir}" -name '*.pot' -exec rm \{} \;
}

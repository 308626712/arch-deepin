# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-pygtk
_pkgname=pygtk
pkgver=@PKGVER_FIXED@
_pkgver=@PKGVER_SRC@
pkgrel=6
pkgdesc="Python bindings for the GTK widget set - with patches to fix memory leak for deepin-ui"
arch=('i686' 'x86_64')
url="http://www.pygtk.org/"
license=('LGPL')
groups=('deepin')
depends=('libglade' 'python2-cairo' 'pygobject')
optdepends=('python2-numpy')

source=("@SOURCE_ORIG@"
        "@SOURCE_DEBIAN@"
        "python27.patch"
        )
sha256sums=('@SHA256SUM_ORIG@'
            '@SHA256SUM_DEBIAN@'
            '39a30456cba055a452bb55c74ef1ff2f5f7bfaad22855b4dd569ab009b56b682')
provides=("${_pkgname}=2.24.0")
conflicts=("${_pkgname}")

prepare() {
  cd "${_pkgname}-${_pkgver}"

  # apply python27 patch
  # https://bugzilla.gnome.org/show_bug.cgi?id=623965
  patch -Np1 -i "${srcdir}/python27.patch"

  # apply deepin patches
  patchdir="${srcdir}"/debian/patches
  for f in $(< "${patchdir}"/series); do
    echo "==> patching: ${f##*/}"
    # ignore error if patch again
    patch -Np1 -i "${patchdir}/${f}" || echo "==> patch error: ${f##*/}"
  done

}

build() {
  cd "${_pkgname}-${_pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 gtk/gtk-extrafuncs.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
  sed -i -e 's#env python$#env python2#' "${pkgdir}"/usr/lib/pygtk/2.0/{,demos/}*.py
}

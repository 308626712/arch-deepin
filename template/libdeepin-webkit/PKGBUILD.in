# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=libdeepin-webkit
pkgver=@PKGVER_FIXED@
_srcdirname=@SRCDIRNAME@
pkgrel=2
pkgdesc='A fork of webkit-gtk for Linux Deepin desktop eviroment'
arch=('i686' 'x86_64')
url="http://www.linuxdeepin.com/"
license=('LGPL' 'GPL' 'BSD' 'custom')
groups=('deepin')
depends=('geoclue' 'libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant'
         'libgl' 'gtk3' 'gstreamer0.10-base-plugins' 'libsecret'
         'libwebp' 'harfbuzz-icu')
makedepends=('gtk2' 'gperf' 'gobject-introspection' 'python2' 'mesa'
             'gtk-doc' 'gawk' 'chrpath' 'bison' 'perl' 'flex')
optdepends=('gtk2: Netscape plugin support')
provides=('deepin-webkit')
replaces=('deepin-webkit')
options=(!libtool !emptydirs)
source=("@SOURCE@"
        "webkit-gtk-1.8.2-bison-2.6.patch")
sha256sums=('@SHA256SUM@'
            'cea36d9a955e4f5c2cd2fe2037ff63067217596e88cbbaa1da2ecc7546d3cd44')

prepare() {
  cd "${_srcdirname}"

  patch -p1 -i "${srcdir}"/webkit-gtk-1.8.2-bison-2.6.patch
}

build() {
  cd "${_srcdirname}"

  export CFLAGS="$CFLAGS -Wall -Wl,--as-needed"
  export LDFLAGS="$LDFLAGS -Wl,--no-keep-memory"

  PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --disable-silent-rules \
        --with-gtk=3.0 \
        --disable-webgl
  make
}

package() {
  cd "${_srcdirname}"

  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/{bin,include,share}
  mv "${pkgdir}"/usr/lib/pkgconfig/webkitgtk-3.0.pc "${pkgdir}"/usr/lib/pkgconfig/deepin-webkit-3.0.pc
}

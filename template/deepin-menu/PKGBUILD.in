# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-menu
pkgver=@PKGVER_FIXED@
_pkgver=@PKGVER@
pkgrel=1
pkgdesc="Deepin menu service for building beautiful menus."
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/deepin-menu"
license=('GPL3')
depends=('python2-pyqt5')
makedepends=('python2-setuptools' 'qt5-declarative' 'qt5-x11extras')
groups=('deepin')
source=("@SOURCE@")
sha256sums=('@SHA256SUM@')

prepare() {
  cd "${pkgname}-${_pkgver}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${pkgname}-${_pkgver}"

  python2 setup.py build
  qmake && make
}

package() {
  cd "${pkgname}-${_pkgver}"

  python2 setup.py install --root="${pkgdir}" --optimize=1

  make INSTALL_ROOT="$pkgdir" install

  #install -dm 755 "${pkgdir}"/usr/share/pyshared
  install -dm 755 "${pkgdir}"/usr/share/dbus-1/services/
  install -dm 755 "${pkgdir}"/etc/xdg/autostart/

  mv "${pkgdir}"/usr/deepin_menu "${pkgdir}"/usr/share/pyshared
  chmod +x "${pkgdir}"/usr/share/pyshared/menu.py

  install -m 644 *.service "${pkgdir}"/usr/share/dbus-1/services/
  install -m 644 *.desktop "${pkgdir}"/etc/xdg/autostart/
}

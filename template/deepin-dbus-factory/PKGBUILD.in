# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=deepin-dbus-factory
_pkgbase=dbus-factory
pkgname=('dde-go-dbus-factory'
         'dde-qml-dbus-factory')
pkgver=@PKGVER@
pkgrel=1
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dbus-factory"
license=('GPL3')
makedepends=('go' 'go-dlib' 'qt5-webkit' 'qt5-quickcontrols' 'python2'
             'bluez' 'bluez-libs' 'udisks2' 'upower' 'accountsservice'
             'libdbus' 'deepin-dbus-generator')
groups=('deepin')
options=('staticlibs')
source=("@SOURCE@"
        "fix-arch.patch")
sha256sums=('@SHA256SUM@'
            'b01823357e6a1c7178fade964a87c6e80ad55acb9f15e40f4e3d7edf9022c420')

build() {
  cd "${_pkgbase}-${pkgver}"

  patch -p1 -i ../fix-arch.patch

  make -j$(expr $(nproc) \* 2 - 1) -l$(nproc)
}

package_dde-qml-dbus-factory() {
  pkgdesc='QML DBus factory for DDE'
  depends=('qt5-declarative')
  optdepends=('udisks2: Disk Management Service'
              'bluez: Daemons for the bluetooth protocol stack'
              'upower: Abstraction for enumerating power devices'
              'accountsservice: D-Bus interface for user account query and manipulation'
              'polkit: Application development toolkit for controlling system-wide privileges'
              'libdbus: DBus library'
              'networkmanager: Network Management daemon')

  cd "${_pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install-qml
}

package_dde-go-dbus-factory() {
  pkgdesc='Go-lang DBus factory for DDE'

  cd "${_pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install-golang
}

# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music-player
pkgver=@PKGVER_FIXED@
_pkgver=@PKGVER_SRC@
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('any')
url="https://gitcafe.com/Deepin/deepin-music"
license=('GPL3')
depends=('python2')
makedepends=('deepin-gettext-tools')
groups=('deepin-extra')
install="${pkgname}.install"
source=("@SOURCE@")
sha256sums=('@SHA256SUM@')

prepare() {
  cd "${pkgname}-${_pkgver}"

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${pkgname}-${_pkgver}"

  deepin-generate-mo tools/locale_config.ini
}

package() {
  depends=('gstreamer0.10-python' 'gstreamer0.10-bad-plugins'
           'gstreamer0.10-good-plugins' 'gstreamer0.10-ugly-plugins'
           'mutagen' 'python2-chardet' 'python2-pyquery' 'python2-cssselect'
           'python2-dbus' 'sonata' 'cddb-py' 'python2-scipy' 'python2-pycurl'
           'python2-xlib' 'python2-keybinder2' 'python2-deepin-ui' 'desktop-file-utils')

  cd "${pkgname}-${_pkgver}"

  make DESTDIR="${pkgdir}" PREFIX="/usr" install
}

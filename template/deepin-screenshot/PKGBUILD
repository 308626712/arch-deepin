# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Maintainer: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screenshot
pkgver=3.0.20150410165450
_srcdirname=deepin-screenshot-3.0+20150410165450~8775c7849a
pkgrel=1
pkgdesc="Easy-to-use screenshot tool for linuxdeepin desktop environment"
arch=('any')
url="https://gitcafe.com/Deepin/deepin-screenshot"
license=('GPL3')
makedepends=('deepin-gettext-tools')
groups=('deepin-extra')
install="${pkgname}.install"
source=("http://packages.linuxdeepin.com/deepin/pool/main/d/deepin-screenshot/deepin-screenshot_3.0+20150410165450~8775c7849a.tar.gz")
sha256sums=('cc1f7de62a817145faaf3e8d4bb83eabae24404bc3f2ef499fb80cf9937d4e14')

prepare() {
  cd "${_srcdirname}"

  # fix python version
  find src -type f | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd "${_srcdirname}"

  make
}

package() {
  depends=('deepin-menu' 'deepin-qml-widgets' 'deepin-pygtk'
           'python2' 'python2-pyqt5' 'python2-wnck' 'python2-xpybutil')

  cd "${_srcdirname}"
  make DESTDIR="${pkgdir}" install
}

 # Local Variables:
 # sh-basic-offset: 2
 # End:
